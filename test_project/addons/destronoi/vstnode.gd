@tool
class_name VSTNodeGD
"""
Author: George Power <george@georgepower.dev>
"""
## A node in a Voronoi Subdivision Tree.
##
## Despite the name, [VSTNodeGD] does [b]not[/b] inheret from [Node] and
## therefore cannot be used as a scene object. A [VSTNodeGD] is used in the 
## construction of a Voronoi Subdivison Tree (VST). A VST functions as a binary
## tree where a [VSTNodeGD] contains a [MeshInstance3D] and 0 to 2 children. A 
## [VSTNodeGD] without children is considered a leaf.

## An enum to define laterality. A root VSTNodeGD would have no laterality as it
## has no parent VSTNodes. Any child VSTNodeGD must have a laterality of left or right.
enum Laterality {NONE = 0, LEFT, RIGHT}
## The ArrayMesh data of the current VSTNodeGD. If not the root node of the VST,
## this data is generated by [method DestronoiNode.bisect].
var _mesh: Mesh = null
var _mat: Material = null
## An array of sites. A site is a 3-D point in space, typically placed within
## the volume of the mesh.[br]A VSTNodeGD has 0 or 2 sites. A pair of sites are used
## to define a 3-D plane for use in [method DestronoiNode.bisect].
var _sites: PackedVector3Array
## The left child node.
var _left: VSTNodeGD = null
## The right child node.
var _right: VSTNodeGD = null
## The depth level in the VST. The root is the only node at level 0.
var _level: int = 0
## The laterality of the current VSTNodeGD in relation to its parent. 
var _laterality: int = Laterality.NONE

## Initializes a VSTNodeGD using mesh data, a depth level, and a laterality value. 
func _init(mesh:Mesh, mat:Material, level: int = 0, lat: int = Laterality.NONE):
	_mesh = mesh.duplicate()
	_mat = mat.duplicate()
	_level = level
	_laterality = lat

## Returns the number of sites. Should be 0 or 2, otherwise something has gone wrong.
func get_site_count() -> int:
	return _sites.size()

## Recursively populates [param out_arr] with each leaf [VSTNodeGD].
## [br]Returns an [Array] containing the [VSTNodeGD]s. Returns an empty [Array]
## if there are no child [VSTNodeGD]s.
## [br]For proper usage: [br][param root] must be a valid [VSTNodeGD]. [br][param out_arr] must be a valid (preferably empty) [Array]. [br]For instance:
## [br][code]myVSTNode.get_leaf_nodes()[/code] is improper usage and will return an empty array.
## [br][code]myVSTNode.get_leaf_nodes(myVSTNode, myArray)[/code] is valid.
func get_leaf_nodes(root: VSTNodeGD = null, out_arr: Array = []) -> Array:
	if(root == null):
		return [];
	if(root._left == null && root._right == null):
		out_arr.append(root)
		return [root]
	if(root._left != null):
		get_leaf_nodes(root._left, out_arr)
	if(root._right != null):
		get_leaf_nodes(root._right, out_arr)
	return []

## Recursively populates [param out_arr] with [VSTNodeGD]s of right [enum Laterality] at a certain depth level [param lim].
## [br]Returns an array containing the leaf VSTNodes. Returns an empty array if
## there are no child VSTNodes with right [enum Laterality].
## [br]If [param lim] exceeds the maximum value of [param level], the VSTNodes 
## with a depth of [param level] will be returned.
## [br][br]For proper usage: [br][param root] must be a valid VSTNodeGD [br][param level] and [param lim] must be positive integers. [br]For instance:
## [br][code]myVSTNode.get_right_leaf_nodes()[/code] is improper usage and will return an empty array.
## [br][code]myVSTNode.get_right_leaf_nodes(myVSTNode, myArray)[/code] is valid.
## [br][code]myVSTNode.get_right_leaf_nodes(myVSTNode, myArray, 3)[/code] is valid.
## [br][code]myVSTNode.get_right_leaf_nodes(myVSTNode, myArray, 2, 0)[/code] is valid.
func get_right_leaf_nodes(root: VSTNodeGD = null, out_arr: Array = [], lim: int = 1, level: int = 0) -> Array:
	if(root == null):
		return [];
	if(root._left == null && root._right == null) || level == lim:
		out_arr.append(root)
		return [root]
	if(root._left != null && level > 0):
		get_right_leaf_nodes(root._left, out_arr, lim, level+1)
	if(root._right != null):
		get_right_leaf_nodes(root._right, out_arr, lim, level+1)
	return []

## Recursively populates [param out_arr] with [VSTNodeGD]s of left [enum Laterality] at a certain depth level [param lim].
## [br]Returns an array containing the leaf VSTNodes. Returns an empty array if
## there are no child VSTNodes with left [enum Laterality].
## [br]If [param lim] exceeds the maximum value of [param level], the VSTNodes 
## with a depth of [param level] will be returned.
## [br][br]For proper usage: [br][param root] must be a valid VSTNodeGD [br][param level] and [param lim] must be positive integers. [br]For instance:
## [br][code]myVSTNode.get_left_leaf_nodes()[/code] is improper usage and will return an empty array.
## [br][code]myVSTNode.get_left_leaf_nodes(myVSTNode, myArray)[/code] is valid.
## [br][code]myVSTNode.get_left_leaf_nodes(myVSTNode, myArray, 3)[/code] is valid.
## [br][code]myVSTNode.get_left_leaf_nodes(myVSTNode, myArray, 2, 0)[/code] is valid.
func get_left_leaf_nodes(root: VSTNodeGD = null, out_arr: Array = [], lim: int = 1, level: int = 0) -> Array:
	if(root == null):
		return [];
	if(root._left == null && root._right == null) || level == lim:
		out_arr.append(root)
		return [root]
	if(root._left != null):
		get_left_leaf_nodes(root._left, out_arr, lim, level+1)
	if(root._right != null && level > 0):
		get_left_leaf_nodes(root._right, out_arr, lim, level+1)
	return []

func _to_string():
	return "VSTNodeGD {mesh}".format({"mesh":_mesh})
